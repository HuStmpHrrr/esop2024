<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Algebra.Properties.Monoid.Mult</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- Multiplication over a monoid (i.e. repeated addition)</a>
<a class="Comment" id="163">------------------------------------------------------------------------</a>

<a class="Symbol" id="237">{-#</a> <a class="Keyword" id="241">OPTIONS</a> <a class="Pragma" id="249">--cubical-compatible</a> <a class="Pragma" id="270">--safe</a> <a class="Symbol" id="277">#-}</a>

<a class="Keyword" id="282">open</a> <a class="Keyword" id="287">import</a> <a class="Module" href="Algebra.Bundles.html" id="294">Algebra.Bundles</a> <a class="Keyword" id="310">using</a> <a class="Symbol" id="316">(</a><a class="Record" href="Algebra.Bundles.html#6016" id="317">Monoid</a><a class="Symbol" id="323">)</a>
<a class="Keyword" id="325">open</a> <a class="Keyword" id="330">import</a> <a class="Module" href="Data.Nat.Base.html" id="337">Data.Nat.Base</a> <a class="Symbol" id="351">as</a> <a class="Module" id="354">ℕ</a> <a class="Keyword" id="356">using</a> <a class="Symbol" id="362">(</a><a class="Datatype" href="Agda.Builtin.Nat.html#186" id="363">ℕ</a><a class="Symbol" id="364">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#204" id="366">zero</a><a class="Symbol" id="370">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="372">suc</a><a class="Symbol" id="375">;</a> <a class="Record" href="Data.Nat.Base.html#2570" id="377">NonZero</a><a class="Symbol" id="384">)</a>
<a class="Keyword" id="386">open</a> <a class="Keyword" id="391">import</a> <a class="Module" href="Relation.Binary.html" id="398">Relation.Binary</a>
<a class="Keyword" id="414">open</a> <a class="Keyword" id="419">import</a> <a class="Module" href="Relation.Binary.PropositionalEquality.html" id="426">Relation.Binary.PropositionalEquality</a> <a class="Symbol" id="464">as</a> <a class="Module" id="467">P</a> <a class="Keyword" id="469">using</a> <a class="Symbol" id="475">(</a><a class="Datatype Operator" href="Agda.Builtin.Equality.html#133" id="476">_≡_</a><a class="Symbol" id="479">)</a>

<a class="Keyword" id="482">module</a> <a class="Module" href="Algebra.Properties.Monoid.Mult.html" id="489">Algebra.Properties.Monoid.Mult</a> <a class="Symbol" id="520">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#521" id="521">a</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#523" id="523">ℓ</a><a class="Symbol" id="524">}</a> <a class="Symbol" id="526">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#527" id="527">M</a> <a class="Symbol" id="529">:</a> <a class="Record" href="Algebra.Bundles.html#6016" id="531">Monoid</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#521" id="538">a</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#523" id="540">ℓ</a><a class="Symbol" id="541">)</a> <a class="Keyword" id="543">where</a>

<a class="Comment" id="550">-- View of the monoid operator as addition</a>
<a class="Keyword" id="593">open</a> <a class="Module" href="Algebra.Bundles.html#6016" id="598">Monoid</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#527" id="605">M</a>
  <a class="Keyword" id="609">renaming</a>
  <a class="Symbol" id="620">(</a> <a class="Field Operator" href="Algebra.Bundles.html#6145" id="622">_∙_</a>       <a class="Symbol" id="632">to</a> <a class="Field Operator" id="635">_+_</a>
  <a class="Symbol" id="641">;</a> <a class="Function" href="Algebra.Structures.html#1216" id="643">∙-cong</a>    <a class="Symbol" id="653">to</a> <a class="Function" id="656">+-cong</a>
  <a class="Symbol" id="665">;</a> <a class="Function" href="Algebra.Structures.html#1427" id="667">∙-congʳ</a>   <a class="Symbol" id="677">to</a> <a class="Function" id="680">+-congʳ</a>
  <a class="Symbol" id="690">;</a> <a class="Function" href="Algebra.Structures.html#1366" id="692">∙-congˡ</a>   <a class="Symbol" id="702">to</a> <a class="Function" id="705">+-congˡ</a>
  <a class="Symbol" id="715">;</a> <a class="Function" href="Algebra.Structures.html#4037" id="717">identityˡ</a> <a class="Symbol" id="727">to</a> <a class="Function" id="730">+-identityˡ</a>
  <a class="Symbol" id="744">;</a> <a class="Function" href="Algebra.Structures.html#4098" id="746">identityʳ</a> <a class="Symbol" id="756">to</a> <a class="Function" id="759">+-identityʳ</a>
  <a class="Symbol" id="773">;</a> <a class="Function" href="Algebra.Structures.html#2878" id="775">assoc</a>     <a class="Symbol" id="785">to</a> <a class="Function" id="788">+-assoc</a>
  <a class="Symbol" id="798">;</a> <a class="Field" href="Algebra.Bundles.html#6172" id="800">ε</a>         <a class="Symbol" id="810">to</a> <a class="Field" id="813">0#</a>
  <a class="Symbol" id="818">)</a>

<a class="Keyword" id="821">open</a> <a class="Keyword" id="826">import</a> <a class="Module" href="Relation.Binary.Reasoning.Setoid.html" id="833">Relation.Binary.Reasoning.Setoid</a> <a class="Function" href="Algebra.Structures.html#1291" id="866">setoid</a>

<a class="Keyword" id="874">open</a> <a class="Keyword" id="879">import</a> <a class="Module" href="Algebra.Definitions.html" id="886">Algebra.Definitions</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="906">_≈_</a>

<a class="Comment" id="911">------------------------------------------------------------------------</a>
<a class="Comment" id="984">-- Definition</a>

<a class="Keyword" id="999">open</a> <a class="Keyword" id="1004">import</a> <a class="Module" href="Algebra.Definitions.RawMonoid.html" id="1011">Algebra.Definitions.RawMonoid</a> <a class="Function" href="Algebra.Bundles.html#6409" id="1041">rawMonoid</a> <a class="Keyword" id="1051">public</a>
  <a class="Keyword" id="1060">using</a> <a class="Symbol" id="1066">(</a><a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1067">_×_</a><a class="Symbol" id="1070">)</a>

<a class="Comment" id="1073">------------------------------------------------------------------------</a>
<a class="Comment" id="1146">-- Properties of _×_</a>

<a id="×-congʳ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1168" id="1168">×-congʳ</a> <a class="Symbol" id="1176">:</a> <a class="Symbol" id="1178">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1180" id="1180">n</a> <a class="Symbol" id="1182">→</a> <a class="Symbol" id="1184">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1180" id="1185">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1187">×_</a><a class="Symbol" id="1189">)</a> <a class="Function Operator" href="Relation.Binary.Core.html#1572" id="1191">Preserves</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1201">_≈_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1572" id="1205">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1207">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1168" id="1211">×-congʳ</a> <a class="Number" id="1219">0</a>       <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1227" id="1227">x≈x′</a> <a class="Symbol" id="1232">=</a> <a class="Function" href="Relation.Binary.Structures.html#1591" id="1234">refl</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1168" id="1239">×-congʳ</a> <a class="Symbol" id="1247">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="1248">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1252" id="1252">n</a><a class="Symbol" id="1253">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1255" id="1255">x≈x′</a> <a class="Symbol" id="1260">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#656" id="1262">+-cong</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1255" id="1269">x≈x′</a> <a class="Symbol" id="1274">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1168" id="1275">×-congʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1252" id="1283">n</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1255" id="1285">x≈x′</a><a class="Symbol" id="1289">)</a>

<a id="×-cong"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1292" id="1292">×-cong</a> <a class="Symbol" id="1299">:</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1301">_×_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1698" id="1305">Preserves₂</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#133" id="1316">_≡_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1698" id="1320">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1322">_≈_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1698" id="1326">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1328">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1292" id="1332">×-cong</a> <a class="Symbol" id="1339">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1340" id="1340">n</a><a class="Symbol" id="1341">}</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#190" id="1343">P.refl</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1350" id="1350">x≈x′</a> <a class="Symbol" id="1355">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1168" id="1357">×-congʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1340" id="1365">n</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1350" id="1367">x≈x′</a>

<a id="×-congˡ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1373" id="1373">×-congˡ</a> <a class="Symbol" id="1381">:</a> <a class="Symbol" id="1383">∀</a> <a class="Symbol" id="1385">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1386" id="1386">x</a><a class="Symbol" id="1387">}</a> <a class="Symbol" id="1389">→</a> <a class="Symbol" id="1391">(</a><a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1392">_×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1386" id="1395">x</a><a class="Symbol" id="1396">)</a> <a class="Function Operator" href="Relation.Binary.Core.html#1572" id="1398">Preserves</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#133" id="1408">_≡_</a> <a class="Function Operator" href="Relation.Binary.Core.html#1572" id="1412">⟶</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1414">_≈_</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1373" id="1418">×-congˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1426" id="1426">m≡n</a> <a class="Symbol" id="1430">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1292" id="1432">×-cong</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1426" id="1439">m≡n</a> <a class="Function" href="Relation.Binary.Structures.html#1591" id="1443">refl</a>

<a class="Comment" id="1449">-- _×_ is homomorphic with respect to _ℕ+_/_+_.</a>

<a id="×-homo-+"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1498" id="1498">×-homo-+</a> <a class="Symbol" id="1507">:</a> <a class="Symbol" id="1509">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1511" id="1511">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1513" id="1513">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1515" id="1515">n</a> <a class="Symbol" id="1517">→</a> <a class="Symbol" id="1519">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1513" id="1520">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#319" id="1522">ℕ.+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1515" id="1526">n</a><a class="Symbol" id="1527">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1529">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1511" id="1531">x</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1533">≈</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1513" id="1535">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1537">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1511" id="1539">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1541">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1515" id="1543">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1545">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1511" id="1547">x</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1498" id="1549">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1558" id="1558">x</a> <a class="Number" id="1560">0</a>       <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1568" id="1568">n</a> <a class="Symbol" id="1570">=</a> <a class="Function" href="Relation.Binary.Structures.html#1617" id="1572">sym</a> <a class="Symbol" id="1576">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#730" id="1577">+-identityˡ</a> <a class="Symbol" id="1589">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1568" id="1590">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1592">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1558" id="1594">x</a><a class="Symbol" id="1595">))</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1498" id="1598">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1607">x</a> <a class="Symbol" id="1609">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="1610">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1614">m</a><a class="Symbol" id="1615">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1617">n</a> <a class="Symbol" id="1619">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#1925" id="1621">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1629">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1631">+</a> <a class="Symbol" id="1633">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1634">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#319" id="1636">ℕ.+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1640">n</a><a class="Symbol" id="1641">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1643">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1645">x</a>    <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1650">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#656" id="1653">+-cong</a> <a class="Function" href="Relation.Binary.Structures.html#1591" id="1660">refl</a> <a class="Symbol" id="1665">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1498" id="1666">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1675">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1677">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1679">n</a><a class="Symbol" id="1680">)</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1682">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1686">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1688">+</a> <a class="Symbol" id="1690">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1691">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1693">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1695">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1697">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1699">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1701">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1703">x</a><a class="Symbol" id="1704">)</a>  <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1707">≈⟨</a> <a class="Function" href="Relation.Binary.Structures.html#1617" id="1710">sym</a> <a class="Symbol" id="1714">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#788" id="1715">+-assoc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1723">x</a> <a class="Symbol" id="1725">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1726">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1728">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1730">x</a><a class="Symbol" id="1731">)</a> <a class="Symbol" id="1733">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1734">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1736">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1738">x</a><a class="Symbol" id="1739">))</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1742">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1746">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1748">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1614" id="1750">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1752">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1754">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1756">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1617" id="1758">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1760">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1607" id="1762">x</a>    <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#2564" id="1767">∎</a>

<a id="×-idem"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1770" id="1770">×-idem</a> <a class="Symbol" id="1777">:</a> <a class="Symbol" id="1779">∀</a> <a class="Symbol" id="1781">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1782" id="1782">c</a><a class="Symbol" id="1783">}</a> <a class="Symbol" id="1785">→</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1787">_+_</a> <a class="Function Operator" href="Algebra.Definitions.html#3400" id="1791">IdempotentOn</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1782" id="1804">c</a> <a class="Symbol" id="1806">→</a>
         <a class="Symbol" id="1817">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1819" id="1819">n</a> <a class="Symbol" id="1821">→</a> <a class="Symbol" id="1823">.{{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1826" id="1826">_</a> <a class="Symbol" id="1828">:</a> <a class="Record" href="Data.Nat.Base.html#2570" id="1830">NonZero</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1819" id="1838">n</a><a class="Symbol" id="1839">}}</a> <a class="Symbol" id="1842">→</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1819" id="1844">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1846">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1782" id="1848">c</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="1850">≈</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1782" id="1852">c</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1770" id="1854">×-idem</a> <a class="Symbol" id="1861">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1862" id="1862">c</a><a class="Symbol" id="1863">}</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1865" id="1865">idem</a> <a class="Symbol" id="1870">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="1871">suc</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#204" id="1875">zero</a><a class="Symbol" id="1879">)</a>    <a class="Symbol" id="1884">=</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#759" id="1886">+-identityʳ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1862" id="1898">c</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#1770" id="1900">×-idem</a> <a class="Symbol" id="1907">{</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="1908">c</a><a class="Symbol" id="1909">}</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1911" id="1911">idem</a> <a class="Symbol" id="1916">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="1917">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1921" id="1921">n</a><a class="Symbol" id="1922">@(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="1924">suc</a> <a class="Symbol" id="1928">_))</a> <a class="Symbol" id="1932">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#1925" id="1934">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="1942">c</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1944">+</a> <a class="Symbol" id="1946">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1921" id="1947">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="1949">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="1951">c</a><a class="Symbol" id="1952">)</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1954">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#705" id="1957">+-congˡ</a> <a class="Symbol" id="1965">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#1770" id="1966">×-idem</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1911" id="1973">idem</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1921" id="1978">n</a> <a class="Symbol" id="1980">)</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1982">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="1986">c</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="1988">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="1990">c</a>       <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="1998">≈⟨</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1911" id="2001">idem</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="2006">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#1908" id="2010">c</a>           <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#2564" id="2022">∎</a>

<a id="×-assocˡ"></a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#2025" id="2025">×-assocˡ</a> <a class="Symbol" id="2034">:</a> <a class="Symbol" id="2036">∀</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2038" id="2038">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2040" id="2040">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2042" id="2042">n</a> <a class="Symbol" id="2044">→</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2040" id="2046">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2048">×</a> <a class="Symbol" id="2050">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2042" id="2051">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2053">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2038" id="2055">x</a><a class="Symbol" id="2056">)</a> <a class="Field Operator" href="Algebra.Bundles.html#6116" id="2058">≈</a> <a class="Symbol" id="2060">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2040" id="2061">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#522" id="2063">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2042" id="2067">n</a><a class="Symbol" id="2068">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2070">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2038" id="2072">x</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#2025" id="2074">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2083" id="2083">x</a> <a class="InductiveConstructor" href="Agda.Builtin.Nat.html#204" id="2085">zero</a>    <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2093" id="2093">n</a> <a class="Symbol" id="2095">=</a> <a class="Function" href="Relation.Binary.Structures.html#1591" id="2097">refl</a>
<a class="Function" href="Algebra.Properties.Monoid.Mult.html#2025" id="2102">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2111">x</a> <a class="Symbol" id="2113">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="2114">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2118">m</a><a class="Symbol" id="2119">)</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2121">n</a> <a class="Symbol" id="2123">=</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#1925" id="2125">begin</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2133">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2135">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2137">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="2139">+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2141">m</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2143">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2145">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2147">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2149">x</a>     <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="2155">≈⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#705" id="2158">+-congˡ</a> <a class="Symbol" id="2166">(</a><a class="Function" href="Algebra.Properties.Monoid.Mult.html#2025" id="2167">×-assocˡ</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2176">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2178">m</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2180">n</a><a class="Symbol" id="2181">)</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1061" id="2183">⟩</a>
  <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2187">n</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2189">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2191">x</a> <a class="Field Operator" href="Algebra.Properties.Monoid.Mult.html#635" id="2193">+</a> <a class="Symbol" id="2195">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2196">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#522" id="2198">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2202">n</a><a class="Symbol" id="2203">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2205">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2207">x</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1162" id="2209">≈˘⟨</a> <a class="Function" href="Algebra.Properties.Monoid.Mult.html#1498" id="2213">×-homo-+</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2222">x</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2224">n</a> <a class="Symbol" id="2226">(</a><a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2227">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#522" id="2229">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2233">n</a><a class="Symbol" id="2234">)</a> <a class="Function" href="Relation.Binary.Reasoning.Setoid.html#1162" id="2236">⟩</a>
  <a class="Symbol" id="2240">(</a><a class="InductiveConstructor" href="Agda.Builtin.Nat.html#217" id="2241">suc</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2118" id="2245">m</a> <a class="Primitive Operator" href="Agda.Builtin.Nat.html#522" id="2247">ℕ.*</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2121" id="2251">n</a><a class="Symbol" id="2252">)</a> <a class="Function Operator" href="Algebra.Definitions.RawMonoid.html#1089" id="2254">×</a> <a class="Bound" href="Algebra.Properties.Monoid.Mult.html#2111" id="2256">x</a>     <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Single.html#2564" id="2262">∎</a>
</pre></div></body></html>