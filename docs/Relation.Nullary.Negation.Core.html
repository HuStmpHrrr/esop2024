<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Relation.Nullary.Negation.Core</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- Core properties related to negation</a>
<a class="Comment" id="145">------------------------------------------------------------------------</a>

<a class="Symbol" id="219">{-#</a> <a class="Keyword" id="223">OPTIONS</a> <a class="Pragma" id="231">--cubical-compatible</a> <a class="Pragma" id="252">--safe</a> <a class="Symbol" id="259">#-}</a>

<a class="Keyword" id="264">module</a> <a class="Module" href="Relation.Nullary.Negation.Core.html" id="271">Relation.Nullary.Negation.Core</a> <a class="Keyword" id="302">where</a>

<a class="Keyword" id="309">open</a> <a class="Keyword" id="314">import</a> <a class="Module" href="Data.Bool.Base.html" id="321">Data.Bool.Base</a> <a class="Keyword" id="336">using</a> <a class="Symbol" id="342">(</a><a class="Function" href="Data.Bool.Base.html#941" id="343">not</a><a class="Symbol" id="346">)</a>
<a class="Keyword" id="348">open</a> <a class="Keyword" id="353">import</a> <a class="Module" href="Data.Empty.html" id="360">Data.Empty</a> <a class="Keyword" id="371">using</a> <a class="Symbol" id="377">(</a><a class="Function" href="Data.Empty.html#895" id="378">⊥</a><a class="Symbol" id="379">)</a>
<a class="Keyword" id="381">open</a> <a class="Keyword" id="386">import</a> <a class="Module" href="Data.Empty.Irrelevant.html" id="393">Data.Empty.Irrelevant</a> <a class="Keyword" id="415">using</a> <a class="Symbol" id="421">(</a><a class="Function" href="Data.Empty.Irrelevant.html#336" id="422">⊥-elim</a><a class="Symbol" id="428">)</a>
<a class="Keyword" id="430">open</a> <a class="Keyword" id="435">import</a> <a class="Module" href="Data.Product.Base.html" id="442">Data.Product.Base</a> <a class="Keyword" id="460">using</a> <a class="Symbol" id="466">(</a><a class="Function Operator" href="Data.Product.Base.html#1118" id="467">_×_</a><a class="Symbol" id="470">;</a> <a class="InductiveConstructor Operator" href="Agda.Builtin.Sigma.html#235" id="472">_,_</a><a class="Symbol" id="475">;</a> <a class="Field" href="Data.Product.Base.html#617" id="477">proj₁</a><a class="Symbol" id="482">;</a> <a class="Field" href="Data.Product.Base.html#631" id="484">proj₂</a><a class="Symbol" id="489">)</a>
<a class="Keyword" id="491">open</a> <a class="Keyword" id="496">import</a> <a class="Module" href="Data.Sum.Base.html" id="503">Data.Sum.Base</a> <a class="Keyword" id="517">using</a> <a class="Symbol" id="523">(</a><a class="Datatype Operator" href="Data.Sum.Base.html#625" id="524">_⊎_</a><a class="Symbol" id="527">;</a> <a class="Function Operator" href="Data.Sum.Base.html#811" id="529">[_,_]</a><a class="Symbol" id="534">;</a> <a class="InductiveConstructor" href="Data.Sum.Base.html#675" id="536">inj₁</a><a class="Symbol" id="540">;</a> <a class="InductiveConstructor" href="Data.Sum.Base.html#700" id="542">inj₂</a><a class="Symbol" id="546">)</a>
<a class="Keyword" id="548">open</a> <a class="Keyword" id="553">import</a> <a class="Module" href="Function.Base.html" id="560">Function.Base</a> <a class="Keyword" id="574">using</a> <a class="Symbol" id="580">(</a><a class="Function" href="Function.Base.html#1638" id="581">flip</a><a class="Symbol" id="585">;</a> <a class="Function Operator" href="Function.Base.html#2003" id="587">_$_</a><a class="Symbol" id="590">;</a> <a class="Function Operator" href="Function.Base.html#1115" id="592">_∘_</a><a class="Symbol" id="595">;</a> <a class="Function" href="Function.Base.html#725" id="597">const</a><a class="Symbol" id="602">)</a>
<a class="Keyword" id="604">open</a> <a class="Keyword" id="609">import</a> <a class="Module" href="Level.html" id="616">Level</a>

<a class="Keyword" id="623">private</a>
  <a class="Keyword" id="633">variable</a>
    <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#646" id="646">a</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="648">p</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#650" id="650">q</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#652" id="652">w</a> <a class="Symbol" id="654">:</a> <a class="Postulate" href="Agda.Primitive.html#742" id="656">Level</a>
    <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#666" id="666">A</a> <a class="Symbol" id="668">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="670">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#646" id="674">a</a>
    <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="680">P</a> <a class="Symbol" id="682">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="684">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="688">p</a>
    <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="694">Q</a> <a class="Symbol" id="696">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="698">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#650" id="702">q</a>
    <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#708" id="708">Whatever</a> <a class="Symbol" id="717">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="719">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#652" id="723">w</a>

<a class="Comment" id="726">------------------------------------------------------------------------</a>
<a class="Comment" id="799">-- Negation.</a>

<a class="Keyword" id="813">infix</a> <a class="Number" id="819">3</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="821">¬_</a>
<a id="¬_"></a><a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="824">¬_</a> <a class="Symbol" id="827">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="829">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#646" id="833">a</a> <a class="Symbol" id="835">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="837">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#646" id="841">a</a>
<a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="843">¬</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#845" id="845">P</a> <a class="Symbol" id="847">=</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#845" id="849">P</a> <a class="Symbol" id="851">→</a> <a class="Function" href="Data.Empty.html#895" id="853">⊥</a>

<a class="Comment" id="856">-- Double-negation</a>
<a id="DoubleNegation"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#875" id="875">DoubleNegation</a> <a class="Symbol" id="890">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="892">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="896">p</a> <a class="Symbol" id="898">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="900">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="904">p</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#875" id="906">DoubleNegation</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#921" id="921">P</a> <a class="Symbol" id="923">=</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="925">¬</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="927">¬</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#921" id="929">P</a>

<a class="Comment" id="932">-- Stability under double-negation.</a>
<a id="Stable"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#968" id="968">Stable</a> <a class="Symbol" id="975">:</a> <a class="Primitive" href="Agda.Primitive.html#388" id="977">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="981">p</a> <a class="Symbol" id="983">→</a> <a class="Primitive" href="Agda.Primitive.html#388" id="985">Set</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#648" id="989">p</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#968" id="991">Stable</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#998" id="998">P</a> <a class="Symbol" id="1000">=</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1002">¬</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1004">¬</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#998" id="1006">P</a> <a class="Symbol" id="1008">→</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#998" id="1010">P</a>

<a class="Comment" id="1013">------------------------------------------------------------------------</a>
<a class="Comment" id="1086">-- Relationship to product and sum</a>

<a class="Keyword" id="1122">infixr</a> <a class="Number" id="1129">1</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#1137" id="1131">_¬-⊎_</a>
<a id="_¬-⊎_"></a><a class="Function Operator" href="Relation.Nullary.Negation.Core.html#1137" id="1137">_¬-⊎_</a> <a class="Symbol" id="1143">:</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1145">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1147">P</a> <a class="Symbol" id="1149">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1151">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1153">Q</a> <a class="Symbol" id="1155">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1157">¬</a> <a class="Symbol" id="1159">(</a><a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1160">P</a> <a class="Datatype Operator" href="Data.Sum.Base.html#625" id="1162">⊎</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1164">Q</a><a class="Symbol" id="1165">)</a>
<a class="Function Operator" href="Relation.Nullary.Negation.Core.html#1137" id="1167">_¬-⊎_</a> <a class="Symbol" id="1173">=</a> <a class="Function Operator" href="Data.Sum.Base.html#811" id="1175">[_,_]</a>

<a class="Comment" id="1182">------------------------------------------------------------------------</a>
<a class="Comment" id="1255">-- Uses of negation</a>

<a id="contradiction"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1276" id="1276">contradiction</a> <a class="Symbol" id="1290">:</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1292">P</a> <a class="Symbol" id="1294">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1296">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1298">P</a> <a class="Symbol" id="1300">→</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#708" id="1302">Whatever</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1276" id="1311">contradiction</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1325" id="1325">p</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1327" id="1327">¬p</a> <a class="Symbol" id="1330">=</a> <a class="Function" href="Data.Empty.Irrelevant.html#336" id="1332">⊥-elim</a> <a class="Symbol" id="1339">(</a><a class="Bound" href="Relation.Nullary.Negation.Core.html#1327" id="1340">¬p</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1325" id="1343">p</a><a class="Symbol" id="1344">)</a>

<a id="contradiction₂"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1347" id="1347">contradiction₂</a> <a class="Symbol" id="1362">:</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1364">P</a> <a class="Datatype Operator" href="Data.Sum.Base.html#625" id="1366">⊎</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1368">Q</a> <a class="Symbol" id="1370">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1372">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1374">P</a> <a class="Symbol" id="1376">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1378">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1380">Q</a> <a class="Symbol" id="1382">→</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#708" id="1384">Whatever</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1347" id="1393">contradiction₂</a> <a class="Symbol" id="1408">(</a><a class="InductiveConstructor" href="Data.Sum.Base.html#675" id="1409">inj₁</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1414" id="1414">p</a><a class="Symbol" id="1415">)</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1417" id="1417">¬p</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1420" id="1420">¬q</a> <a class="Symbol" id="1423">=</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#1276" id="1425">contradiction</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1414" id="1439">p</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1417" id="1441">¬p</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1347" id="1444">contradiction₂</a> <a class="Symbol" id="1459">(</a><a class="InductiveConstructor" href="Data.Sum.Base.html#700" id="1460">inj₂</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1465" id="1465">q</a><a class="Symbol" id="1466">)</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1468" id="1468">¬p</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1471" id="1471">¬q</a> <a class="Symbol" id="1474">=</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#1276" id="1476">contradiction</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1465" id="1490">q</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1471" id="1492">¬q</a>

<a id="contraposition"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1496" id="1496">contraposition</a> <a class="Symbol" id="1511">:</a> <a class="Symbol" id="1513">(</a><a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1514">P</a> <a class="Symbol" id="1516">→</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1518">Q</a><a class="Symbol" id="1519">)</a> <a class="Symbol" id="1521">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1523">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1525">Q</a> <a class="Symbol" id="1527">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1529">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1531">P</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1496" id="1533">contraposition</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1548" id="1548">f</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1550" id="1550">¬q</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1553" id="1553">p</a> <a class="Symbol" id="1555">=</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#1276" id="1557">contradiction</a> <a class="Symbol" id="1571">(</a><a class="Bound" href="Relation.Nullary.Negation.Core.html#1548" id="1572">f</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1553" id="1574">p</a><a class="Symbol" id="1575">)</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1550" id="1577">¬q</a>

<a class="Comment" id="1581">-- Note also the following use of flip:</a>
<a class="Keyword" id="1621">private</a>
  <a id="note"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1631" id="1631">note</a> <a class="Symbol" id="1636">:</a> <a class="Symbol" id="1638">(</a><a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1639">P</a> <a class="Symbol" id="1641">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1643">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1645">Q</a><a class="Symbol" id="1646">)</a> <a class="Symbol" id="1648">→</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1650">Q</a> <a class="Symbol" id="1652">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1654">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1656">P</a>
  <a class="Function" href="Relation.Nullary.Negation.Core.html#1631" id="1660">note</a> <a class="Symbol" id="1665">=</a> <a class="Function" href="Function.Base.html#1638" id="1667">flip</a>

<a class="Comment" id="1673">-- Everything is stable in the double-negation monad.</a>
<a id="stable"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1727" id="1727">stable</a> <a class="Symbol" id="1734">:</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1736">¬</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1738">¬</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#968" id="1740">Stable</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1747">P</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1727" id="1749">stable</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1756" id="1756">¬[¬¬p→p]</a> <a class="Symbol" id="1765">=</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1756" id="1767">¬[¬¬p→p]</a> <a class="Symbol" id="1776">(λ</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1779" id="1779">¬¬p</a> <a class="Symbol" id="1783">→</a> <a class="Function" href="Data.Empty.Irrelevant.html#336" id="1785">⊥-elim</a> <a class="Symbol" id="1792">(</a><a class="Bound" href="Relation.Nullary.Negation.Core.html#1779" id="1793">¬¬p</a> <a class="Symbol" id="1797">(</a><a class="Bound" href="Relation.Nullary.Negation.Core.html#1756" id="1798">¬[¬¬p→p]</a> <a class="Function Operator" href="Function.Base.html#1115" id="1807">∘</a> <a class="Function" href="Function.Base.html#725" id="1809">const</a><a class="Symbol" id="1814">)))</a>

<a class="Comment" id="1819">-- Negated predicates are stable.</a>
<a id="negated-stable"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1853" id="1853">negated-stable</a> <a class="Symbol" id="1868">:</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#968" id="1870">Stable</a> <a class="Symbol" id="1877">(</a><a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1878">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1880">P</a><a class="Symbol" id="1881">)</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1853" id="1883">negated-stable</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1898" id="1898">¬¬¬P</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1903" id="1903">P</a> <a class="Symbol" id="1905">=</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1898" id="1907">¬¬¬P</a> <a class="Symbol" id="1912">(λ</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1915" id="1915">¬P</a> <a class="Symbol" id="1918">→</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1915" id="1920">¬P</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1903" id="1923">P</a><a class="Symbol" id="1924">)</a>

<a id="¬¬-map"></a><a class="Function" href="Relation.Nullary.Negation.Core.html#1927" id="1927">¬¬-map</a> <a class="Symbol" id="1934">:</a> <a class="Symbol" id="1936">(</a><a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1937">P</a> <a class="Symbol" id="1939">→</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1941">Q</a><a class="Symbol" id="1942">)</a> <a class="Symbol" id="1944">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1946">¬</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1948">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#680" id="1950">P</a> <a class="Symbol" id="1952">→</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1954">¬</a> <a class="Function Operator" href="Relation.Nullary.Negation.Core.html#824" id="1956">¬</a> <a class="Generalizable" href="Relation.Nullary.Negation.Core.html#694" id="1958">Q</a>
<a class="Function" href="Relation.Nullary.Negation.Core.html#1927" id="1960">¬¬-map</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1967" id="1967">f</a> <a class="Symbol" id="1969">=</a> <a class="Function" href="Relation.Nullary.Negation.Core.html#1496" id="1971">contraposition</a> <a class="Symbol" id="1986">(</a><a class="Function" href="Relation.Nullary.Negation.Core.html#1496" id="1987">contraposition</a> <a class="Bound" href="Relation.Nullary.Negation.Core.html#1967" id="2002">f</a><a class="Symbol" id="2003">)</a>
</pre></div></body></html>